plugins {
    id 'application'
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id "com.github.maiflai.scalatest" version "0.31"
}


def scalaVersion = findProperty("scalaVersion") ?: "2.12.15"
println "Scala version: $scalaVersion"
def scalaVersionShort = "${VersionNumber.parse(scalaVersion).getMajor()}.${VersionNumber.parse(scalaVersion).getMinor()}"

def sparkVersion = findProperty("sparkVersion") ?: "3.0.3"
println "Spark version: $sparkVersion"


application {
    mainClass = 'ai.student.computing.Main'
}

group 'ai.student'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


dependencies {
    implementation "org.scala-lang:scala-library:$scalaVersion"
    implementation "org.apache.spark:spark-core_$scalaVersionShort:$sparkVersion"
    implementation "org.apache.spark:spark-sql_$scalaVersionShort:$sparkVersion"
    implementation "org.apache.spark:spark-mllib_$scalaVersionShort:$sparkVersion"
    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation 'com.frugalmechanic:scala-optparse_2.12:1.1.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.3'
    implementation 'com.github.housepower:clickhouse-native-jdbc:2.5.6'

    testImplementation "org.scalatest:scalatest_$scalaVersionShort:3.1.0"
    testImplementation "com.vladsch.flexmark:flexmark-all:0.35.10"
    testImplementation "org.mockito:mockito-scala_$scalaVersionShort:1.16.42"
}

shadowJar {
    zip64 true
    mergeServiceFiles("META-INF/services/org.apache.spark.sql.sources.DataSourceRegister")
}